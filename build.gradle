plugins {
    id "dev.architectury.loom" version "1.7-SNAPSHOT" apply false
    id "architectury-plugin" version "3.4-SNAPSHOT"
    id "com.gradleup.shadow" version "8.3.2" apply false

    id "io.github.pacifistmc.forgix" version "1.2.9"
}

architectury {
    minecraft = project.minecraft_version
}

allprojects {
    group = project.maven_group
    version = project.mod_version
}

forgix {
    group = project.maven_group
    mergedJarName = "cooptweaks-${project.mod_version}-fabric-neoforge.jar"
    outputDir = "build"

    if (findProject(":neoforge")) {
        neoforge {
            jarLocation = "build/libs/cooptweaks-neoforge-${project.mod_version}.jar"
        }
    }

    if (findProject(":fabric")) {
        fabric {
            jarLocation = "build/libs/cooptweaks-fabric-${project.mod_version}.jar"
        }
    }
}

subprojects {
    apply plugin: "dev.architectury.loom"
    apply plugin: "architectury-plugin"

    base {
        archivesName = "$project.archives_name-$project.name"
    }

    repositories {
        // Add repositories to retrieve artifacts from in here.
        // You should only use this when depending on other mods because
        // Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
        // See https://docs.gradle.org/current/userguide/declaring_repositories.html
        // for more information about repositories.
    }

    dependencies {
        minecraft "net.minecraft:minecraft:$project.minecraft_version"
        mappings loom.layered {
            it.mappings("net.fabricmc:yarn:$project.yarn_mappings:v2")
            it.mappings("dev.architectury:yarn-mappings-patch-neoforge:$project.yarn_mappings_patch_neoforge_version")
        }
    }

    java {
        withSourcesJar()

        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.VERSION_21
    }

    tasks.withType(JavaCompile).configureEach {
        it.options.release = 21
    }

    build.finalizedBy(mergeJars)
    assemble.finalizedBy(mergeJars)
}
